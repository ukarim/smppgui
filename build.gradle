import java.nio.file.Path

plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    version = '0.1.4'
}

def smppguiFilename = 'smppgui.jar'

jar {
    archiveFileName = smppguiFilename
    metaInf {
        from 'README.md'
        from 'LICENSE.txt'
    }
    manifest {
        attributes('Main-Class': 'com.ukarim.smppgui.Main')
    }
    filesMatching('**/about.txt') { fcp ->
        fcp.filter { line ->
            def date = (new Date()).format('MMM dd, yyyy')
            line.replace('{buildInfo}', "Built on ${date}. Version: ${version}")
        }
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

clean {
    // remove dir created by jlink task
    delete 'smppgui'
}

// This task builds custom jre images with only necessary modules included
tasks.register('jlink', Exec) {
    dependsOn 'build'
    def modulePath = Path.of(buildDir.absolutePath, "libs", smppguiFilename)
    commandLine ([
            'jlink',                                                  // jlink must be preinstalled on your machine
            '--module-path', modulePath,                              // Smppgui jar file
            '--add-modules', 'smppgui',
            '--output', 'smppgui',                                    // Output directory
            '--launcher', 'smppgui=smppgui/com.ukarim.smppgui.Main'   // Launcher shell script. See smppgui/bin directory
    ])
}
